Spring Security uses an [Authentication|https://docs.spring.io/spring-security/site/docs/3.2.x/apidocs/org/springframework/security/core/Authentication.html] object to determine whether the current user has the right to perform a secured action, such as accessing a URL, manipulating a secured domain object, accessing a secured method, and so on. This object is created during login. Typically overlap occurs between the need for authentication data and the need to represent a user in the application in ways that are unrelated to security. The mechanism for populating the authentication is completely pluggable in Spring Security; you only need to provide an implementation of [UserDetailsService|https://docs.spring.io/spring-security/site/docs/3.2.x/apidocs/org/springframework/security/core/userdetails/UserDetailsService.html] and implement its one method, @loadUserByUsername()@.

By default the plugin uses a Grails 'person' domain class to manage this data. @username@, @enabled@, @password@ are the default names of the core required properties. You can easily [plug in your own implementation|guide:userDetailsService], and rename the class, package, and fields. In addition, you should define an @authorities@ property to retrieve roles; this can be a public field or a @getAuthorities()@ method, and it can be defined through a traditional GORM many-to-many or a custom mapping.

Assuming you choose @com.mycompany.myapp@ as your package, and @User@ as your class name, you'll generate this class:

{code}
package com.mycompany.myapp

import groovy.transform.EqualsAndHashCode
import groovy.transform.ToString

@EqualsAndHashCode(includes='username')
@ToString(includes='username', includeNames=true, includePackage=false)
class User implements Serializable {

   private static final long serialVersionUID = 1

   transient springSecurityService

   String username
   String password
   boolean enabled = true
   boolean accountExpired
   boolean accountLocked
   boolean passwordExpired

   User(String username, String password) {
      this()
      this.username = username
      this.password = password
   }

   Set<Role> getAuthorities() {
      UserRole.findAllByUser(this)*.role
   }

   def beforeInsert() {
      encodePassword()
   }

   def beforeUpdate() {
      if (isDirty('password')) {
         encodePassword()
      }
   }

   protected void encodePassword() {
      password = springSecurityService?.passwordEncoder ?
         springSecurityService.encodePassword(password) :
         password
   }

   static transients = ['springSecurityService']

   static constraints = {
      username blank: false, unique: true
      password blank: false
   }

   static mapping = {
      password column: '`password`'
   }
}
{code}

Optionally, add other properties such as @email@, @firstName@, @lastName@, and convenience methods, and so on:

{code}
package com.mycompany.myapp

import groovy.transform.EqualsAndHashCode
import groovy.transform.ToString

@EqualsAndHashCode(includes='username')
@ToString(includes='username', includeNames=true, includePackage=false)
class User implements Serializable {

   private static final long serialVersionUID = 1

   transient springSecurityService

   String username
   String password
   boolean enabled = true
   String email
   String firstName
   String lastName
   boolean accountExpired
   boolean accountLocked
   boolean passwordExpired

   User(String username, String password) {
      this()
      this.username = username
      this.password = password
   }

   def someMethod {
      ...
   }

   Set<Role> getAuthorities() {
      UserRole.findAllByUser(this)*.role
   }

   def beforeInsert() {
      encodePassword()
   }

   def beforeUpdate() {
      if (isDirty('password')) {
         encodePassword()
      }
   }

   protected void encodePassword() {
      password = springSecurityService?.passwordEncoder ?
         springSecurityService.encodePassword(password) :
         password
   }

   static transients = ['springSecurityService']

   static constraints = {
      username blank: false, unique: true
      password blank: false
   }

   static mapping = {
      password column: '`password`'
   }
}
{code}

The @getAuthorities()@ method is analagous to defining @static hasMany = \[authorities: Authority\]@ in a traditional many-to-many mapping. This way @GormUserDetailsService@ can call @user.authorities@ during login to retrieve the roles without the overhead of a bidirectional many-to-many mapping.

The class and property names are configurable using these configuration attributes:

{table}
*Property* | *Default Value* | *Meaning*
userLookup.userDomainClassName | none | User class name
userLookup.usernamePropertyName | 'username' | User class username field
userLookup.passwordPropertyName | 'password' | User class password field
userLookup.authoritiesPropertyName | 'authorities' | User class role collection field
userLookup.enabledPropertyName | 'enabled' | User class enabled field
userLookup.accountExpiredPropertyName | 'accountExpired' | User class account expired field
userLookup.accountLockedPropertyName | 'accountLocked' | User class account locked field
userLookup.passwordExpiredPropertyName | 'passwordExpired' | User class password expired field
userLookup.authorityJoinClassName | 'PersonAuthority' | User/Role many-many join class name
{table}
